<!-- Inline from https://github.com/dondi/bazaar/tree/master/shadow-by-projection-webgl -->
<!doctype html>
<html>
    <head>
        <meta charset="UTF-8"/>
        <title>Projected Shadows in WebGL</title>
        <style>
        canvas {
            border: solid 1px rgb(200, 200, 255);
            display: block;
            margin-left: auto;
            margin-right: auto;
        }
        </style>
    </head>
    <body>
        <!-- Note the rectangular canvas now. -->
        <canvas id="matrices-webgl" width="1024" height="512"></canvas>
<script>
// Polyfill jQuery
const $ = (selector) => ({
    text () {
        return document.querySelector(selector).innerText;
    }
});
</script>

        <!-- GLSLUtilities is a starting point for holding functionality that
             is common to many WebGL programs. -->
        <script src="glsl-utilities.js"></script>

        <!-- Shapes is a library that generates "canned" shapes. -->
        <script src="shapes.js"></script>

        <!-- Set up shaders: we've placed the source in script elements for
            simplicity.  Shaders are ideally kept in separate files for easier
            maintenance, but in that situation, additional code and security
            constraints are involved. -->

        <script id="vertex-shader" type="x-shader/x-vertex">
            #ifdef GL_ES
            precision highp float;
            #endif

            attribute vec3 vertexPosition;

            // Note this new additional output.
            attribute vec3 vertexColor;
            varying vec4 finalVertexColor;
            uniform mat4 projectionMatrix;
            uniform mat4 modelViewMatrix;

            void main(void) {
                gl_Position = projectionMatrix * modelViewMatrix * vec4(vertexPosition, 1.0);
                finalVertexColor = vec4(vertexColor, 1.0);
            }
        </script>

        <script id="fragment-shader" type="x-shader/x-fragment">
            #ifdef GL_ES
            precision highp float;
            #endif

            varying vec4 finalVertexColor;

            void main(void) {
                // We vary the color based on the fragment's z coordinate,
                // which, at this point, ranges from 0 (near) to 1 (far).
                // Note the ".rgb" subselector.
                gl_FragColor = vec4((1.0 - gl_FragCoord.z) * finalVertexColor.rgb, 1.0);
            }
        </script>

        <!-- The vertex shader for shadows. -->
        <script id="shadow-vertex-shader" type="x-shader/x-vertex">
            #ifdef GL_ES
            precision highp float;
            #endif

            attribute vec3 vertexPosition;

            // Note this new additional output.
            uniform mat4 projectionMatrix;
            uniform mat4 modelViewMatrix;

            void main(void) {
                // To fully expose the transformations involved, we lay them out inline.
                // In practice, this is best built from a framework or library, with
                // parameters declared as uniform or attribute variables.
                float ground = -2.0;
                vec3 lightPosition = vec3(1.0, 10.0, 1.0);

                mat4 translateToGround = mat4(
                        vec4(1.0, 0.0, 0.0, 0.0),
                        vec4(0.0, 1.0, 0.0, 0.0),
                        vec4(0.0, 0.0, 1.0, 0.0),
                        vec4(0.0, ground, 0.0, 1.0)
                    );

                mat4 translateToLight = mat4(
                        vec4(1.0, 0.0, 0.0, 0.0),
                        vec4(0.0, 1.0, 0.0, 0.0),
                        vec4(0.0, 0.0, 1.0, 0.0),
                        vec4(lightPosition, 1.0)
                    );

                // The secret sauce!
                mat4 projectShadow = mat4(
                        vec4(1.0, 0.0, 0.0, 0.0),
                        vec4(0.0, 1.0, 0.0, -1.0 / lightPosition.y),
                        vec4(0.0, 0.0, 1.0, 0.0),
                        vec4(0.0, 0.0, 0.0, 0.0)
                    );

                mat4 translateFromLight = mat4(
                        vec4(1.0, 0.0, 0.0, 0.0),
                        vec4(0.0, 1.0, 0.0, 0.0),
                        vec4(0.0, 0.0, 1.0, 0.0),
                        vec4(-lightPosition, 1.0)
                    );

                gl_Position = projectionMatrix *
                    translateToGround * translateToLight * projectShadow * translateFromLight *
                    modelViewMatrix * vec4(vertexPosition, 1.0);
            }
        </script>

        <!-- The fragment shader for shadows. -->
        <script id="shadow-fragment-shader" type="x-shader/x-fragment">
            #ifdef GL_ES
            precision highp float;
            #endif

            void main(void) {
                gl_FragColor = vec4(0.0, 0.0, 0.0, 0.7);
            }
        </script>

        <!-- This script sets up the specific scene for this page. -->
        <script src="shadow-by-projection-webgl.js"></script>
  </body>
</html>
